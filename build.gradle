import org.gradle.internal.os.OperatingSystem

plugins {
  id 'java-library'
  id 'maven-publish'
  id 'com.jfrog.bintray' version '1.8.5'
  id "io.franzbecker.gradle-lombok" version "4.0.0"
}

version "${version}"

project.ext.javaFxPlatform = "${javaFxPlatform}"

allprojects {
  version "${version}"
  sourceCompatibility = JavaVersion.VERSION_14
  targetCompatibility = JavaVersion.VERSION_14

  repositories {
    jcenter()
  }
}

java {
  withSourcesJar()
  withJavadocJar()
}

bintray {
  user = System.getenv('BINTRAY_USER')
  key = System.getenv('BINTRAY_KEY')
  publications = ['mavenJava']
  pkg {
    repo = 'maven'
    name = 'fxstage'
    licenses = ['GPL-3.0']
    vcsUrl = 'https://github.com/micheljung/fxstage.git'
    githubRepo = 'micheljung/fxstage'
    publicDownloadNumbers = true
  }
}

if (javaFxPlatform == "unspecified") {
  switch (OperatingSystem.current()) {
    case OperatingSystem.LINUX:
      project.ext.javaFxPlatform = "linux"
      break
    case OperatingSystem.MAC_OS:
      project.ext.javaFxPlatform = "mac"
      break
    case OperatingSystem.WINDOWS:
      project.ext.javaFxPlatform = "win"
      break
  }
}

dependencies {
  api "net.java.dev.jna:jna:${jnaVersion}"
  api "net.java.dev.jna:jna-platform:${jnaVersion}"
  api "org.apache.logging.log4j:log4j-api:${log4j2Version}"

  compileOnly "org.openjfx:javafx-base:${javaFxVersion}:${javaFxPlatform}"
  compileOnly "org.openjfx:javafx-controls:${javaFxVersion}:${javaFxPlatform}"
  compileOnly "org.openjfx:javafx-fxml:${javaFxVersion}:${javaFxPlatform}"
  compileOnly "org.openjfx:javafx-graphics:${javaFxVersion}:${javaFxPlatform}"

  runtimeOnly "org.apache.logging.log4j:log4j-core:${log4j2Version}"
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      groupId 'ch.micheljung.fxstage'
      artifactId 'fxstage'
      version project.version
      from components.java
      pom {
        name = 'FXStage'
        description = 'Borderless UI for JavaFX'
        url = 'https://github.com/micheljung/fxstage'
        licenses {
          license {
            name = 'GNU General Public License, Version 3.0'
            url = 'https://www.gnu.org/licenses/gpl-3.0.html'
          }
        }
        developers {
          developer {
            id = 'mj'
            name = 'Michel Jung'
            email = 'michel.jung89@gmail.com'
          }
        }
        scm {
          connection = 'scm:git:https://github.com/micheljung/fxstage.git'
          developerConnection = 'scm:git:git@github.com:micheljung/fxstage.git'
          url = 'https://github.com/micheljung/fxstage'
        }
      }
    }
  }
}